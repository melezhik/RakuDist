# --------------------------- Variables -------------------------------------------- #

my $user = config()<user>;
my $module = config()<module>;
my $rakudo-version = config()<rakudo-version>;
my $path-to-raku = "/tmp/whateverable/rakudo-moar/$rakudo-version";
my $path-to-zef = "/home/$user/zef";

# --------------------------- Install Rakudo $rakudo-version ------------------------ #

say "Start Rakudo install, version {$rakudo-version}";

package-install ('wget', 'zstd');

directory "/data/whateverable/", %(
  mode => '755'
);

unless "/data/whateverable/{$rakudo-version}".IO ~~ :f {

  bash "cd /data/whateverable/ && wget -q https://whateverable.6lang.org/{$rakudo-version}", %(
    description => "download https://whateverable.6lang.org/{$rakudo-version}"
  )

}

bash "cd /data/whateverable/ && zstd -dqc -- $rakudo-version | tar -x --absolute-names", %(
    description => "unpack {$rakudo-version}"
);


user $user;

directory $path-to-zef, %(
  owner => $user
);


git-scm "https://github.com/ugexe/zef.git", %( 
  to => $path-to-zef,
  user => $user,
);

# --------------------------- Raku Module Test Against Rakudo $rakudo-version ------------------------ #

say "Start Raku module test, module {$module}";


directory-delete "/home/$user/.raku";

directory "/home/$user/.raku", %(
  owner => $user,
  group => $user
);

directory-delete "/home/$user/.zef";

directory "/home/$user/.zef", %(
  owner => $user,
  group => $user
);


bash "$path-to-raku/bin/perl6 --version", %(
  description => "rakudo version",
);

bash "$path-to-raku/bin/perl6 -I $path-to-zef $path-to-zef/bin/zef install $module", %(
  description => "Install $module",
  envvars => %(
    "PATH" => "{%*ENV<PATH>}:$path-to-raku/bin/"
  ),
  debug => False,
  user => $user
);



