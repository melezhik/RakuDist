my $user = config()<user>;


my $directory = "/data/test/{$user}";

my $scm = config()<scm>;

user $user;

directory $directory, %( 
  owner => $user,
  group => $user

);

git-scm $scm, %( 
  to => $directory, 
  user => $user,
);

bash "cd {$directory} && git log --name-status HEAD^..HEAD", %(
  description => "last commit"
);

bash "cd {$directory} && ls -l";

zef "Test::META";

if os() eq 'alpine' {

  # this is needed for alpine rakudo installation
  unless "/bin/zef".IO ~~ :e {
    copy "/opt/rakudo-pkg/share/perl6/core/bin/zef", "/bin/zef"
  }

  # this is needed for alpine rakudo installation
  unless "/bin/perl6".IO ~~ :e {
    copy "/opt/rakudo-pkg/bin/perl6", "/bin/perl6"
  }

}

package-install %(
  alpine => "sqlite-libs",
  debian => "postgresql postgresql-contrib libpq5 libpq-dev"
);

service-enable "postgresql";
service-start "postgresql";

bash 'psql -c "SELECT version();"', %(
  description => "verify postgres installation",
  user => 'postgres'
);

task-run "files/tasks/create-pg-user/", %(
  user  => $user
);


task-run "files/tasks/patch-t-files", %(
  dir  => $directory,
);


zef $directory, %( 
  force => False,
  depsonly => True, 
  notest => True, 
  user => $user 
);

file "/home/$user/.pgpass", %(
  owner => $user,
  group => $user ,
  mode => '0600',
  content => "*:*:*:$user:123"
);

task-run "files/tasks/set-pg-user-password/", %(
  user  => $user,
  password => "123"
);

bash "dropdb {$user}; createdb {$user} && echo db {$user} created", %(
  description => "create test database - {$user}",
  user => $user
);

bash "cd {$directory} && zef test .", %(
  description => "zef test",
  user => $user,
);

