my $user = config()<user>;


my $directory = "/data/test/{$user}";

my $scm = config()<scm>;

user $user;

directory $directory, %( 
  owner => $user,
  group => $user

);

git-scm $scm, %( 
  to => $directory, 
  user => $user,
);

bash "cd {$directory} && git log --name-status HEAD^..HEAD", %(
  description => "last commit"
);

bash "cd {$directory} && ls -l";

if os() eq 'alpine' {

  # this is needed for alpine rakudo installation
  unless "/bin/zef".IO ~~ :e {
    copy "/opt/rakudo-pkg/share/perl6/core/bin/zef", "/bin/zef"
  }

  # this is needed for alpine rakudo installation
  unless "/bin/perl6".IO ~~ :e {
    copy "/opt/rakudo-pkg/bin/perl6", "/bin/perl6"
  }

}

package-install %(
  alpine => "sqlite-libs",
  debian => "postgresql postgresql-contrib libpq5 libpq-dev"
);

service-enable "postgresql";
service-start "postgresql";

bash 'psql -c "SELECT version();"', %(
  description => "verify postgres installation",
  user => 'postgres'
);

task-run "files/tasks/create-pg-user/", %(
  user  => $user
);


my %state = task-run "files/tasks/patch-t-files", %(
  dir  => $directory,
);

my $dbnum = %state<dbnum>;


zef $directory, %( 
  force => False,
  depsonly => True, 
  user => $user,
  description => "Install module dependencies"
);

file "/home/$user/.pgpass", %(
  owner => $user,
  group => $user ,
  mode => '0600',
  content => "*:*:*:$user:123"
);

task-run "files/tasks/set-pg-user-password/", %(
  user  => $user,
  password => "123"
);

for 0 .. $dbnum-1 -> $i {
  my $dbname = "red{$i}";
  bash "dropdb {$dbname}; createdb {$dbname} && echo db {$dbname} created", %(
    description => "create test database - {$dbname}",
    user => $user
  );
}

bash "cd {$directory} && zef test . --verbose", %(
  description => "zef test",
  user => $user,
);

