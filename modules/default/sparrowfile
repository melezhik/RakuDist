my $user = config()<user>;
my $module = config()<module>;
my $rakudo-version = config()<rakudo_version>;
my $dir = $*CWD;
my $patch-dir = "$dir/files/patches/$module";

# ------------------------------------------- INSTALL Rakudo -------------------------------------------------------- #

say "<<< PREPARE Environment >>>";

user $user;

bash "rm -rf /home/$user/.perl6/*", %(
  description => "clean ~/.perl6 for user $user",
  user => $user
);

bash "rm -rf /home/$user/.raku/*", %(
  description => "clean ~/.raku for user $user",
  user => $user
);

zef "https://github.com/melezhik/sparrow6-rakudo-install.git", %( force => True );

module-run 'Rakudo::Install', %(
  user => $user,
  rakudo-version => $rakudo-version,
);

# ------------------------------------------- INSTALL Module -------------------------------------------------------- #

say "<<< INSTALL Module >>>";

if os() eq 'alpine' {

  # this is needed for alpine rakudo installation
  unless "/bin/zef".IO ~~ :e {
    copy "/opt/rakudo-pkg/share/perl6/core/bin/zef", "/bin/zef"
  }

  # this is needed for alpine rakudo installation
  unless "/bin/perl6".IO ~~ :e {
    copy "/opt/rakudo-pkg/bin/perl6", "/bin/perl6"
  }


}

my %state = task-run "zef fetch {$module}", "zef-fetch", %(
  identity => $module,
  user => $user,
);

my $directory = %state<directory>;

if "{$patch-dir}/sparrowfile".IO ~~ :f {
  say "*** patch dir: {$patch-dir}, patch found ***";
  say "*** execute patch from {$patch-dir}/sparrowfile ***";
  # post deployment checks
  chdir $patch-dir;
  EVALFILE "sparrowfile";
  chdir $dir;
} else {
  say "*** patch dir: {$patch-dir}, no patch found ***";
}

if "{$patch-dir}/depends.raku".IO ~~ :f {
  say "*** install Raku modules from {$patch-dir}/depends.raku file ***";
  # install Raku modules from depends.raku file
  for "{$patch-dir}/depends.raku".IO.lines -> $line {
    next if $line ~~ /^^ \s* '#' /;
    my @params = $line.split(/\s+/);
    my $module = @params.shift;
    next unless $line ~~ /\S/;
    zef $module, %(
      user => $user,
      notest => @params.Set{'notest'} ?? True !! False
    );    
  }
}

if "{$directory}/.rakudist/sparrowfile".IO ~~ :f {
  say "*** execute custom scenario from .rakudist/sparrowfile ***";
  bash "mkdir -p {$directory}/.rakudist/conf && cp -v {%*ENV<SP6_CONFIG>} {$directory}/.rakudist/conf/", %(
    description => "copy configuartion file",
  );
  # custom installation logic, for example external libraries
  chdir "{$directory}/.rakudist/";
  EVALFILE "sparrowfile";
  chdir $dir;
}

if "{$directory}/.rakudist/depends.raku".IO ~~ :f {
  say "*** install Raku modules from .rakudist/depends.raku file ***";
  # install Raku modules from depends.raku file
  for "{$directory}/.rakudist/depends.raku".IO.lines -> $line {
    next if $line ~~ /^^ \s* '#' /;
    my @params = $line.split(/\s+/);
    my $module = @params.shift;
    next unless $line ~~ /\S/;
    zef $module, %(
      user => $user,
      notest => @params.Set{'notest'} ?? True !! False
    );    
  }
}


bash "cd {$directory} && zef install .", %(
  description => "zef install $module",
  user => $user,
  debug => False
);


if "{$directory}/.rakudist/smokefile".IO ~~ :f {
  say "*** execute smoke test scenario from .rakudist/smokefile ***";
  bash "mkdir -p {$directory}/.rakudist/conf && cp -v {%*ENV<SP6_CONFIG>}  {$directory}/.rakudist/conf/", %(
    description => "copy configuartion file",
  );
  # post deployment checks
  chdir "$directory/.rakudist/";
  EVALFILE "smokefile";
  chdir $dir;
}

run-tomty-tests("{$directory}") if "{$directory}/.tomty".IO ~~ :d;
run-tomty-tests("{$directory}/.rakudist") if "{$directory}/.rakudist/.tomty".IO ~~ :d;

if "{$patch-dir}/.tomty".IO ~~ :d {

  say "***  Tomty tests found at {$patch-dir}/.tomty ***";

  directory-delete "{$directory}/tmp/";

  bash "cp -r {$patch-dir} {$directory}/tmp && chown -R $user {$directory}/tmp", %(
    description => "copy {$patch-dir} to {$directory}/tmp",
  );

  run-tomty-tests("{$directory}/tmp");

}

say "===";
say "RakuDist: OK";

sub run-tomty-tests ( $path ) {

  zef "Tomty", %(
    notest => True,
    user => config()<user>,
  );
  say "*** Run tomty tests from $path/.tomty ***";
  bash "tomty --init && tomty --all --verbose", %(
    description => "tomty test",
    user => config()<user>,
    envvars => %( SP6_REPO => 'http://repo.westus.cloudapp.azure.com/' ),
    cwd => $path
  );

}

